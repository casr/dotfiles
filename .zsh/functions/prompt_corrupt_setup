prompt_corrupt_help() {
  cat <<EOM
Similar to the pure prompt but with less magic
EOM
}

prompt_corrupt_human_time() {
    local human total_seconds=$1
    local days=$(( total_seconds / 60 / 60 / 24 ))
    local hours=$(( total_seconds / 60 / 60 % 24 ))
    local minutes=$(( total_seconds / 60 % 60 ))
    local seconds=$(( total_seconds % 60 ))

    (( days > 0 )) && human+="${days}d "
    (( hours > 0 )) && human+="${hours}h "
    (( minutes > 0 )) && human+="${minutes}m "
    human+="${seconds}s"

    [ ${1:-0} -ge 5 ] && print -n -- $human
}

prompt_corrupt_preexec() {
  timer=${timer:-$SECONDS}
}

prompt_corrupt_precmd() {
  vcs_info

  local runtime
  if [ $timer ]; then
    psvar[8]=

    runtime=$(($SECONDS - $timer))
    [ $runtime -ge 5 ] && psvar[8]=${runtime}
    unset timer
  fi
}

prompt_corrupt_setup() {
  local cwd='%F{blue}%~%f'
  local prompt_symbol='%(?.%F{magenta}.%F{red})‚ùØ%f '
  local runtime_last_command

  runtime_last_command='$([ ${psvar[8]:-0} -ge 5 ] && prompt_corrupt_human_time $psvar[8])'

  local -ah ps1
  ps1=(
    $prompt_newline
    $cwd
    ' %F{magenta}$vcs_info_msg_0_%f '
    $runtime_last_command
    $prompt_newline
    $prompt_symbol
  )

  PROMPT="${(j..)ps1}"
  RPROMPT='$(__get_vi_mode)'
  PROMPT_EOL_MARK=

  add-zsh-hook precmd prompt_corrupt_precmd
  add-zsh-hook preexec prompt_corrupt_preexec

  autoload -Uz vcs_info
  zstyle ':vcs_info:*' enable cvs hg git svn
  zstyle ':vcs_info:*' formats "%b "
  zstyle ':vcs_info:*' actionformats "%b (%a) "

  prompt_opts=(cr sp percent subst)
}

prompt_corrupt_setup "$@"
